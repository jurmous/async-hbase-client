apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
group = 'org.mousio'
version = '0.8.0-SNAPSHOT'

project.ext {
    mavenDeployUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/';
    hbaseVersion = '0.98.7-hadoop2'
}

if (version.endsWith("-SNAPSHOT")) {
    mavenDeployUrl = 'https://oss.sonatype.org/content/repositories/snapshots/';
}

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
    }
    test {
        java {
            srcDir 'src/test/java'
        }
    }
}

dependencies {
    compile 'io.netty:netty-codec-http:4.0.23.Final'
    compile('org.apache.hbase:hbase-client:' + hbaseVersion) {
        exclude group: 'org.slf4j'
    }

    testCompile sourceSets.main.compileClasspath
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile 'org.slf4j:slf4j-simple:1.7.7'
    testCompile 'org.apache.hbase:hbase-testing-util:' + hbaseVersion
    testCompile 'org.apache.hadoop:hadoop-minicluster:2.2.0'
}

javadoc {
    source sourceSets.main.allJava
}

jar {
    from sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: mavenDeployUrl) {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'async-hbase-client'
                packaging 'jar'
                description 'An asynchronous Hbase client'
                url 'https://github.com/jurmous/async-hbase-client'

                scm {
                    url 'scm:git@github.com:jurmous/async-hbase-client.git'
                    connection 'scm:git@github.com:jurmous/async-hbase-client.git'
                    developerConnection 'scm:git@github.com:jurmous/async-hbase-client.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'jurmous'
                        name 'Jurriaan Mous'
                    }
                }
            }
        }
    }
}